Let's make a game!
	name:Recursion Universe
	by:boshki0987
	desc:Version 0.0.24 (alpha). Latest updates will be displayed soon.
	created:1/6/2020
	updated:2/13/2020
//*buyup and *buydown are the buttons to change bulk buy amount, which is represented by *buyamount. *asteroid is the only building affected by bulk buy (other than buyamount changing the cost of all buildings)

Settings
	building cost increase:100%
	building cost refund:0%
	background:https://blenderartists.org/uploads/default/original/4X/7/e/2/7e2d7bea4ac21388c4a96e1371f375c4ce00094b.jpg

CSS
	.objectbox
	{
	    width: 100%;
	}

Layout
	*main
		contains:res, buttons
	*res
		contains:Resources
		class:fullWidth
	*buttons
		contains:Buttons
	*store
		contains:buildings, upgrades
	*buildings
		contains:tag:object, tag:objectbutton
		header:Objects
		tooltip origin:left
	*upgrades
		contains:Upgrades
		costs:hide
		names:hide
	*generatorsbox
		in:upgrades
		contains:tag:generators
		header:Generators
	*abilitiesbox
		contains:tag:abilities
		header:Abilities
		in:upgrades
	*exoticbox
		contains:tag:exoticupgrades
		in:upgrades
		header:Exotic Upgrades
		
Buttons
	*wormhole
		name:Wormhole
		desc:Open up a wormhole to convert all of the matter and dust you have into exotic matter. This will destroy all dust and objects you have, and will use all of your energy.<//><b>Activating a wormhole will give you [pow(dust,0.4)] exotic matter.<b>
		on click:
			yield pow(dust,0.4) exotic
			lose :Buildings
			lose :Upgrades:notTag:essentials
			dust is 25
			energy is 0
			:tag:cooldowns is 0
		end
		req:have generator4
	*convertbutton
		name:Convert Energy
		desc:Activate 10% of your exotic matter, and convert it into energy. This ability has a 1 hour cooldown, and needs [convertcooldown] seconds until it can be used again. Converting right now will give you [pow((exotic/10),0.15)*1000] energy.
		tag:abilities
		on click:
			if (convertcooldown = 0)
				yield pow((exotic/10),0.15)*1000 energy
				lose exotic/10 exotic
				convertcooldown is 3600
			else
				toast Your cooldown needs to be at 0!
			end
		end
		passive:if (convertcooldown > 0) 
	*buyup
		name:Buy x10
		on click:if (buyamount < 1e10) buyamount is buyamount*10
	*buydown
		name:Buy x0.1
		on click:if (buyamount > 1) buyamount is buyamount*0.1



	*TEMPLATE
		tag:objectbutton
		class:objectbox
	*asteroidbutton
		name:Create Asteroid
		text:Create Asteroid ([10*buyamount] dust, [buyamount] energy)
		on click:
			if (dust >= 10*buyamount and energy >= buyamount)
				yield buyamount asteroid
				lose 10*buyamount dust
				lose 1*buyamount energy
			end
		end
	*planetbutton
		name:Create Planet (1000 dust, 2 energy)
		text:Create Planet ([1000*buyamount] dust, [2*buyamount] energy)
		on click:
			if (dust >= 1000*buyamount and energy >= 2*buyamount)
				yield buyamount planet
				lose 1000*buyamount dust
				lose 2*buyamount energy
			end
		end
	*starbutton
		name:Create Star (1 million dust, 3 energy)
		text:Create Star ([1e6*buyamount] dust, [3*buyamount] energy)
		on click:
			if (dust >= 1e6*buyamount and energy >= 3*buyamount)
				yield buyamount star
				lose 1e6*buyamount dust
				lose 3*buyamount energy
			end
		end
	*nebulabutton
		name:Create Nebula (10 billion dust, 4 energy)
		text:Create Nebula ([1e10*buyamount] dust, [4*buyamount] energy)
		on click:
			if (dust >= 1e10*buyamount and energy >= 4*buyamount)
				yield buyamount nebula
				lose 1e10*buyamount dust
				lose 4*buyamount energy
			end
		end
	*galaxybutton
		name:Create Galaxy (1 quadrillion dust, 5 energy)
		text:Create Galaxy ([1e15*buyamount] dust, [5*buyamount] energy)
		on click:
			if (dust >= 1e15*buyamount and energy >= 5*buyamount)
				yield buyamount galaxy
				lose 1e15*buyamount dust
				lose 5*buyamount energy
			end
		end
		
Resources
 	*buyamount
		start with:1
		on load:this is 1
		passive:multiply cost of :Buildings by this
	*dust
		name:Dust
		desc:Cosmic dust can be used to make many different celestial objects, however more and more dust is needed to make larger things.
		show earned
		start with:25
		on load:dust is 10
	*energy
		name:Energy
		desc:Energy is essential for making celestial bodies form from gravity power. This energy is often limited, and finding ways to speed up energy production is hard.
	*exotic
		name:Exotic Matter
		desc:Exotic matter obtained from converting normal matter is extremely valuable, boosting your dust earn by an additive 1%. It can also be used to activate special boosts. Dust mass is currently being boosted by x[1+(0.01*this)].
		passive:multiply yield of dust by 1+(0.01*this)
	*TEMPLATE
		tag:object
		class:objectbox
	*asteroid
		name:Asteroid
		desc:A small rocky body that slowly generates more dust.
		on tick:yield this dust
	*planet
		name:Planet
		on tick:yield this asteroid
		desc:A large rocky body that attracts asteroids with its gravity force.
	*star
		name:Star
		on tick:yield this planet
		desc:A hot, gas-filled body that has a gravity strong enough to attract planets big and small.
	*nebula
		name:Nebula
		on tick:yield this star
		desc:A large cloud of gas and dust, serving as a nursery for stars and star systems.
	*galaxy
		name:Galaxy
		on tick:yield this nebula
		desc:A huge formation of thousands and thousands of nebulae and stars.
	*TEMPLATE
		on tick:lose this
		hidden
		tag:cooldowns
	*convertcooldown
	*mattercooldown

Buildings
	*cluster
		name:Cluster
		on tick:yield 1 galaxy
		cost:1e22 dust, 6 energy
		desc:A galaxy cluster gathers groups of galaxies and nebulae, eventually forming more material.
	*supercluster
		name:Supercluster
		on tick:yield 1 cluster
		cost:1e31 dust, 7 energy
		desc:A supercluster is a small web in the universe made of countless clusters.
	*filament
		name:Filament
		on tick:yield 1 supercluster
		cost:1e42 dust, 8 energy
		desc:Galaxy filaments are the largest structures in the universe, the biggest ones being billions of light years wide.  
	*universe
		name:Universe
		on tick:yield 1 filament
		cost:1e55 dust, 9 energy
		desc:The enormous realms that contain every well-known celestial body.
	*multiverse
		name:Multiverse
		on tick:yield 1 universe
		cost:1e70 dust, 10 energy
		desc:A pocket of space that holds many, many universes.

Upgrades
	*generator1
		name:Tiny Generator
		desc:A tiny energy generator produces 1 energy per second.
		on tick:yield 1 energy
		owned
		tag:essentials generators
	*generator2
		name:Small Generator
		desc:A small energy generator produces 2 energy per second.
		on tick:yield 2 energy
		cost:200 energy
		tag:generators
	*generator3
		name:Standard Generator
		desc:A standard energy generator produces 3 energy per second.
		on tick:yield 3 energy
		cost:800 energy
		tag:generators
	*generator4
		name:Large Generator
		desc:A large energy generator produces 4 energy per second.
		on tick:yield 4 energy
		cost:3200 energy
		tag:generators
	*generator5
		name:Colossal Generator
		desc:A colossal energy generator produces 5 energy per second.
		on tick:yield 5 energy
		cost:12800 energy
		tag:generators

Achievements
